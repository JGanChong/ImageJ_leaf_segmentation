/* Macro used to segment leafs/algae untop of a mat 
 * Created and test with ImageJ 1.53g java 1.8.0_172 64bits Johnny Gan behappyftw@g.ucla.edu March 2021
 */

 
//************User Variables************
MedianRadius = 20; //Radius for Median filter

LowerPeakTHR = 30; //Lower threshold from peak to count as object.  
HigherPeakTHR = 50; //Higher threshold from peak to count as object.
//ie if peak is 100 intensity, and lower and higher threshold of 20, then get objects from 80 to 120 in intensity
//If its selecting too many bright objects, reduce HigfherPeakTHR, if not enough, raise it. same for LowerPeakTHR

minsize = 1600; //Minimum size of object to count it
mincircularity = 0.08 //Minimium circularity. Used to removed random straight shadows and also messed up objects that are touching too much

HistogramBins = 128; //Last thing to change. How many bins to find peak. value from 1-256. The less bins the more "smoothing". Peaks used to find intensity of background and intensity of object of interest.

ColorChannel = 3; //Which color to use to segment. 1=red, 2=green, 3=blue.


//************Program************
title=getTitle(); //Get image name
run("Select None");
run("Duplicate...", "title=dup"); //duplicate work copy
run("RGB Stack"); //Convert to RGB stack
setSlice(ColorChannel); //Select channel to segment
run("Duplicate...", "use"); //Duplicate and use selected channel only
run("Median...", "radius="+MedianRadius); //Apply median filter
run("Unsharp Mask...", "radius=30 mask=0.80"); //Unsharp mask to enhance signal

//Find two peaks (1 for object 1 for background)
getHistogram(values, counts, HistogramBins);
peaks=Array.findMaxima(counts,0);
i=0;
while (peaks.length > 2) {
	peaks=Array.findMaxima(counts, i);
	i+=1000;
}
Array.sort(peaks);

//set threshold according to peaks and user set value
setThreshold((peaks[0]*3)-LowerPeakTHR, (peaks[0]*3)+HigherPeakTHR);
//Convert to mask
setOption("BlackBackground", true);
run("Convert to Mask");
run("Fill Holes");
//Analyze and find ROIs
run("Analyze Particles...", "size="+minsize+"-Infinity circularity="+mincircularity+"-1.00 clear add");

selectImage(title);
roiManager("Show All");
close("\\Others");
